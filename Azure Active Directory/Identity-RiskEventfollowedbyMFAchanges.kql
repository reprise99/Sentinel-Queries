// Detects when a user flags an Azure AD risk event followed by changes to their MFA profile - potentially detecting a bad actor changing MFA details
// Timeframe = the time between flagging a risk event and MFA details being changed
let timeframe = 4h;
AADUserRiskEvents
| where TimeGenerated > ago(1d)
| where RiskDetail != "aiConfirmedSigninSafe"
| project RiskTime=iff(isnotempty(ActivityDateTime), ActivityDateTime, TimeGenerated), UserPrincipalName, RiskEventType, RiskLevel, Source
| join kind=inner (
    AuditLogs
    | where TimeGenerated > ago(1d)
    | where OperationName in ("User registered security info", "User deleted security info","User registered all required security info")
    | where Result == "success"
    | extend UserPrincipalName = tostring(TargetResources[0].userPrincipalName)
    | project
        SecurityInfoTime=TimeGenerated,
        OperationName,
        UserPrincipalName,
        Result,
        ResultReason)
    on UserPrincipalName
| project
    RiskTime,
    SecurityInfoTime,
    UserPrincipalName,
    RiskEventType,
    RiskLevel,
    Source,
    OperationName,
    ResultReason
| where (SecurityInfoTime - RiskTime) between (0min .. timeframe)
